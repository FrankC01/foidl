;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; ast_type
; AST type model
;
; Copyright (c) Frank V. Castellucci
; All Rights Reserved
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

module ast_type
include langcorem

include ast_validate

; Make keys standout in source using AST

var EXPRS           :exprs
var IS_PRIVATE      :is_private
var IS_DEFAULT      :is_default
var NAME            :name
var DECLARATIONS    :exprs
var TOKEN           :token
var SOURCE          :source
var TYPE            :type
var COLLTYPE        :colltype
var ARGCNT          :argcnt
var ELMCNT          :argcnt
var ARGLIST         :arglist
var LITTYPE         :littype
var EXTERNS         :externs
var LITERALS        :literals
var LAMBDAS         :lambdas
var PAIRS           :exprs
var PREDICATE       :predicate
var EVAL            :eval_fn

var :private ast_type {
    ;   Reference types
    :varref  {
        TYPE        :varref
        NAME        nil
        EVAL        ref_validate}
    :funcref {
        TYPE        :funcref
        NAME        nil
        ARGCNT      zero
        EVAL        default_validate}
    :funcargref {
        TYPE        :funcargref
        NAME        nil
        EVAL        default_validate}
    :literalref {
        TYPE        :literalref
        NAME        nil
        LITTYPE     nil
        EVAL        ref_validate}
    :match_exprref {
        TYPE        :match_exprref
        NAME        nil
        EVAL        default_validate}
    ;   Declaration types
    ; module symbol declarations*
    :module   {
        TYPE        :module
        NAME        nil
        EXTERNS     nil
        LITERALS    nil
        LAMBDAS     nil
        DECLARATIONS nil
        EVAL        module_validate}
    ; var :private? symbol expression?
    :variable  {
        TYPE        :variable
        NAME        nil
        IS_PRIVATE  false
        EVAL        variable_validate}
    ; func :private? symbol [symbol*] expression*
    :function  {
        TYPE        :function
        NAME        nil
        ARGLIST     nil
        IS_PRIVATE  false
        EVAL        function_validate}
    ;   Expression types
    ; ^[symbol*] expression?
    :lambda  {
        TYPE        :lambda
        NAME        nil
        ARGLIST     nil
        IS_PRIVATE  true
        EVAL        default_validate}
    ; symbol: expression*
    :call {
        TYPE        :call
        NAME        nil
        EVAL        call_validate}
    ; match symbol? expression match_guard+
    :match  {
        TYPE        :match
        NAME        nil
        PREDICATE   nil
        EVAL        default_validate}
    ; let symbol? [let_pair*] expression
    :let  {
        TYPE        :let
        NAME        nil
        ARGLIST     nil
        EVAL        default_validate}
    ; :match_default
    :match_default {
        TYPE        :match_default
        EVAL        default_validate }
    ; ?: pred expression expression
    :if {
        TYPE        :if
        EVAL        if_validate}
    ; | pred expr
    :match_guard {
        TYPE        :match_guard
        IS_DEFAULT  false
        EVAL        default_validate}
    ; @( expression+ )
    :group   {
        TYPE        :group
        EVAL        group_validate}
    ; (expression+)
    :partial  {
        TYPE        :partial
        EVAL        default_validate}
    ; [] {} <> #{}
    :empty_collection   {
        TYPE        :empty_collection
        COLLTYPE    nil
        ELMCNT      zero
        EVAL        emptycollection_validate}
    ; [expression+] {expression expression,...} <expression+> #{expression+}
    :collection         {
        TYPE        :collection
        COLLTYPE    nil
        ELMCNT      zero
        EVAL        collection_validate}
    ; symbol
    :symbol   {
        TYPE        :symbol
        NAME        nil
        EVAL        symbol_validate}
    ; :private
    :private  {
        TYPE        :private
        EVAL        default_validate}
}

; Convenience sets for predicates

var collection_set  #{:empty_collection :collection}
var coltype_set     #{:list :map :set :vector}
var littype_set     #{:string :keyword :integer :real_number :hex :bin :char}
var decl_set        #{:function :variable :include :module}
var ref_set         #{:literalref :funcref :varref}

var name_set #{
    :module
    :function
    :variable
    :symbol
    :lambda
    :call
    :match
    :match_exprref}

var invalid_set #{
    :match_guard
    :function
    :variable
    :rparen
    :include
    :module}

; Predicates

func is_decl [node]
    getd: decl_set get: node TYPE false

func is_named? [node]
    getd: name_set get: node TYPE false

func is_ref? [node]
    getd: ref_set get: node TYPE false

func is_colltype? [node]
    getd: collection_set get: node TYPE false

func decl_or_guard [node]
    or: is_decl: node =: get: node TYPE :match_guard

func has_exprs? [node]
    let exp [] get: node EXPRS
    ?: nil?: exp
        false
        ?: =: count: exp zero
            false
            true

func is_not_decl? [node]
    not: getd: decl_set get: node TYPE false

func is_funclambda_args? [node]
    ?: getd: collection_set get: node TYPE false
        ?: getd: coltype_set get: node COLLTYPE false
            true
            false
        false

; Function: type_instance
; Description: Return a new AST type instance. All are imbued
; with source reference (string) and token
; Syntax: type_instance: typekw source token
;   typekw  - The AST type keyword
;   source  - Name of source file
;   token   - The source file token (has sourcepos, etc.)

func type_instance [typekw source token]
    let typeinst []
        getd: ast_type typekw nil
    ?: nil?: typeinst
        failWith: extend: "Unrecognized AST type " typekw
        let []
            map_extend!:                            ; Extend new instance with token
                extendKV: typeinst SOURCE source    ; Create new instance and extend
                TOKEN token

; Function: type_with_exprs
; Description: Imbues an expression list on the AST type node
; Syntax: type_with_exprs: typekw source token
;   typekw  - The AST type keyword
;   source  - Name of source file
;   token   - The source file token (has sourcepos, etc.)

func type_with_exprs [typekw source token]
    let node [] type_instance: typekw source token
    map_extend!: node EXPRS list_inst!:

; Function: type_literal_ref
; Description: Constructs a literal reference storing the
; base type in modules literal map

func type_literal_ref [mmodule token source]
    let ttype []
        get: token :token_type
    ; Create the literal reference
    let lref []
        map_extend!:
            type_instance: :literalref source token
            LITTYPE ttype
    ; Add to the global (module) literal map
    map_extend!:
        get: get: mmodule LITERALS ttype
        get: token :token_str lref
    ; Return the literal reference
    lref

; Function:
; Description:
; Syntax:

func :private gen_literal_map [mnode]
    fold:
        ^[acc x]
            map_extend!: acc x map_inst!:
        map_inst!: littype_set

; Function:
; Description:
; Syntax:

func type_module_create [source token]
    let mnode []
        type_with_exprs: :module source token

    map_extend!:
        map_extend!:
            mnode
            LITERALS gen_literal_map: mnode
        EXTERNS map_inst!:
