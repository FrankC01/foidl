;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; parse
; Compiler parse and semantic verification
;
; Copyright (c) Frank V. Castellucci
; All Rights Reserved
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

module parse

include langcorem
include ast_type
include parse_utils
include errors

; Parse is responsible for consuming the list of tokens and
; returning an AST for all types found in token list.

; Function:
; Description:
; Syntax:

func :private unhandled_type [pmap token]
    exception:
        format:
            "Parser unhandled type {}"
            [token]

; Function: module_handler
; Description: Finalize mmodule by:
;   Popping stack to get symbol for module
;   Consume the rest of the stack as declarations (assumed)
;   Returning reduced module map

func :private module_handler  [mmodule ast_tree token srcname]
    let msym [] get: get: first: ast_tree :token :token_str
    pop!: ast_tree
    map_extend!:
        map_extend!:
            map_extend!: mmodule NAME msym
            DECLARATIONS ast_tree
        TOKEN token
    reduced: mmodule

; Function: variable_handler
; Description: Finalize 'var' by
;   Instantiate variable AST node
;   Reconcile :private or public
;   Grab expression (TODO allow for 'var varname' only putting in nil)
;   Putting node back in stack

func :private variable_handler  [mmodule ast_tree token srcname]
    let vnode [] type_with_exprs: :variable srcname token
    process_for_private: vnode ast_tree
    get_var_expr: vnode ast_tree
    extend!: ast_tree vnode

; Function: function_handler
; Description: Finalize 'func' by
;   Instantiate function node
;   Reconcile :private or public
;   Verify arglist (collection dependency)
;   Grab expression (TODO allow for 'func funcname []' only putting in nil)
;

func :private function_handler  [mmodule ast_tree token srcname]
    let fnode [] type_with_exprs: :function srcname token
    process_for_private: fnode ast_tree
    verify_func_args: fnode ast_tree
    get_func_exprs: fnode ast_tree
    extend!: ast_tree fnode

; Function: if_handler
; Description: Parse 3 expression flow control

func :private if_handler  [mmodule ast_tree token srcname]
    let inode [] type_with_exprs: :if srcname token
    parse_if: inode ast_tree
    extend!: ast_tree inode


; Function: let_handler
; Description:

func :private let_handler  [mmodule ast_tree token srcname]
    extend!:
        ast_tree
        parse_let:
            type_with_exprs: :let srcname token
            ast_tree

; Function: match_handler
; Description:
; Syntax:

func :private match_handler  [mmodule ast_tree token srcname]
    extend!:
        ast_tree
        parse_match:
            type_with_exprs: :match srcname token
            ast_tree

; Function: match_guard_handler
; Description:
; Syntax:

func :private match_guard_handler  [mmodule ast_tree token srcname]
    extend!:
        ast_tree
        parse_match_guard:
            type_with_exprs: :match_guard srcname token
            ast_tree

; Function: match_exprref_handler
; Description:
; Syntax:

func :private match_exprref_handler  [mmodule ast_tree token srcname]
    extend!:
        ast_tree
        map_extend!:
            type_instance: :match_exprref srcname token
            NAME get: token :token_str

; Function: match_default_handler
; Description:
; Syntax:

func :private match_default_handler  [mmodule ast_tree token srcname]
    extend!:
        ast_tree
        type_instance: :match_default srcname token

; Function: lambda_handler
; Description:
; Syntax:

func :private lambda_handler [mmodule ast_tree token srcname]
    let lnode [] type_with_exprs: :lambda srcname token
    verify_func_args: lnode ast_tree
    extend!: ast_tree get_single_expr: lnode ast_tree

; Function: group_handler
; Description:

func :private group_handler [mmodule ast_tree token srcname]
    let gnode [] type_with_exprs: :group srcname token
    parse_group: token srcname gnode ast_tree
    extend!: ast_tree gnode

; Function: partial_handler
; Description:

func :private partial_handler [mmodule ast_tree token srcname]
    let pnode [] type_with_exprs: :partial srcname token
    parse_partial: token srcname pnode ast_tree
    extend!: ast_tree pnode

; Function: right_paren_handler
; Description: Part of partial processing
; Syntax:

func :private right_paren_handler [mmodule ast_tree token srcname]
    extend!: ast_tree token

; Function: math_ref_handler
; Description: Reference to math operator handler

func :private math_ref_handler [mmodule ast_tree token srcname]
    extend!: ast_tree parse_mathref: get: token :token_str

; Function: math_call_handler
; Description: Handler for calling math function syntax sugar

func :private math_call_handler [mmodule ast_tree token srcname]
    extend!:
        ast_tree
            parse_mathcall:
                type_with_exprs: :call srcname token
                ast_tree
                get: mmodule EXTERNS

; Function: boolpref_handler
; Description: Handler for calling boolean predicate syntax sugar

func :private boolpref_handler [mmodule ast_tree token srcname]
    extend!:
        ast_tree
            parse_boolpredcall:
                type_with_exprs: :call srcname token
                ast_tree
                get: mmodule EXTERNS

; Function: func_call_handler
; Description: Handler for function call token
; Syntax:

func :private func_call_handler [mmodule ast_tree token srcname]
    extend!:
        ast_tree
            parse_functioncall:
                type_with_exprs: :call srcname token
                ast_tree
                get: mmodule EXTERNS

; Function: collection_start_handler
; Description:
; Syntax:

func :private collection_start_handler [mmodule ast_tree token srcname]
    extend!: ast_tree parse_collection: token ast_tree srcname

; Function: collection_end_handler
; Description:
; Syntax:

func :private collection_end_handler [mmodule ast_tree token srcname]
    extend!: ast_tree token

; Function: literal_handler
; Description:
; Syntax:

func :private literal_handler [mmodule ast_tree token srcname]
    extend!:
        ast_tree
        type_literal_ref: mmodule token srcname


; Function: private_handler
; Description:
; Syntax:

func :private private_handler [mmodule ast_tree token srcname]
    extend!:
        ast_tree
        type_instance: :private srcname token

; Function: symbol_handler
; Description:
; Syntax:

func :private symbol_handler [mmodule ast_tree token srcname]
    extend!:
        ast_tree
        map_extend!:
            type_instance: :symbol srcname token
            NAME get: token :token_str

var :private parse_map {
    :module         module_handler
    :variable       variable_handler
    :function       function_handler
    :if_expr        if_handler
    :let            let_handler
    :match          match_handler
    :match_guard    match_guard_handler
    :match_exprref  match_exprref_handler
    :default        match_default_handler
    :lambda         lambda_handler
    :group          group_handler
    :lparen         partial_handler
    :rparen         right_paren_handler
    :math_call      math_call_handler
    :math_ref       math_ref_handler
    :boolpred_call  boolpref_handler
    :funccall       func_call_handler
    :funccall_pred  func_call_handler
    :funccall_bang  func_call_handler
    :langle         collection_start_handler
    :lbracket       collection_start_handler
    :lbrace         collection_start_handler
    :lset           collection_start_handler
    :rbrace         collection_end_handler
    :rbracket       collection_end_handler
    :rangle         collection_end_handler
    :private        private_handler
    :string         literal_handler
    :char           literal_handler
    :bit            literal_handler
    :hex            literal_handler
    :integer        literal_handler
    :real_number    literal_handler
    :keyword        literal_handler
    :symbol_pred    symbol_handler
    :symbol_bang    symbol_handler
    :symbol         symbol_handler

}

func :private parse_dispatch [mmodule srcname ast_tree token]
    ;printnl!: token
    let d_fn [] getd: parse_map get: token :token_type unhandled_type
    (d_fn mmodule ast_tree token srcname)

func parse [token_list source]
    let mmodule [] type_module_create: source nil
    fold: (parse_dispatch mmodule source) list_inst!: token_list

