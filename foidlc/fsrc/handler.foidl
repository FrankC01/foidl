;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; handler
; Compilation handler builder
;
; Copyright (c) Frank V. Castellucci
; All Rights Reserved
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

module handler

include langcorem

include lexer
include token_utils
include symbol_table
include ast_type
include print_ast
include parse
include errors

var selfhost_comment ";;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Generated by foidlc - Self-hosted foidl compiler
; Copyright (c) Frank V. Castellucci
; All rights reserved
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
`n"

; Write out the top comment block
func :private write_comment_block [ochan]
    write!: ochan selfhost_comment

; Write out top comment block followed by module statement
func :private write_comment_block_and_module [ochan modname]
    write_comment_block: ochan
    write!: ochan format: "module {}`n" [modname]

; Function: module_name_validation
; Description: Takes top entry from token list and validates
; it is a module statement and returns it. If not, report error
; Syntax: module_name_validation: toks
;   toks - Token list

func :private module_name_validation [toks]
    let [first_tok first: toks
         mname_tok second: toks]
        ?: and: =: get: first_tok :token_type :module
                =: get: mname_tok :token_type :symbol
            mname_tok
            token_exception:
                first_tok
                format:
                    "Module statement must be first in source, found {}"
                    [get: first_tok :token_type]

; Function: token_printer
; Description: Prints token list
; Syntax: token_printer: state
;   state - Command line flags and state settings

func :private token_print [state]
    fold:
        ^[acc el]
            @(
                write!: cout el
                write!: cout nlchr
            )
        nil get: state :tokens
    state

; Function: lex_source
; Description: Lexical scan of source file and initial validation
; Syntax: lex_source: cmap source
;   keep_empty - Prevents fail on empty token file
;   state - Command line flags and state settings

func :private lex_source [keep_empty state]
    let [tokens lex: get: state :source #{:comment} keep_empty
         mname_tok module_name_validation: tokens]
            map_extend!:
                map_extend!: state :module_name get: mname_tok :token_str
                :tokens tokens

; Function: setup_output
; Description: Opens a channel for indicated output or uses
; cout and sets in cmap, which is returned
; Syntax: setup_output: state
;   state - Command line flags and state settings

func :private setup_output [state]
    let output [] get: get: state :cmap :output
    ?: output
        map_extend!: state :outchannel
                open!: {
                    :channel output
                    :open :w
                    :buffer :block
                    :write_handler :char }
        map_extend!: state :outchannel cout

; Function: setup_symboltable
; Description: Establishes the compilation unit context
; for symbol management. This is only called when
; compiling
; Syntax: setup_symboltable
;   state - Command line flags and state settings
;

func :private setup_symboltable [state]
    st_compilation_unit: get: state :module_name
    state

; Include processing

var :private include_rthdrs <"foidlrt" "langcore">
var :private include_tail ".defs"
var :private include_existserr " Unable to resolve include file: "

var :private include_cache set_inst!:
var :private include_paths list_inst!:

; Function: capture_includes
; Description: Local capture of include paths when
; command is to compile

func :private capture_includes [state]
    fold:
        ^[acc clineinc]  list_extend!: acc clineinc
        include_paths get: get: state :cmap :includes
    state

; Function: resolve_include_location
; Description: For each file, append the fail (.defs) extension
; and search each of the include paths (-I ...) for file existence
; Syntax: resolve_include_location: incfile
;   incfile - primary name of include file to resolve
;

func :private resolve_include_location [incfile]
    let fullfile [] extend: incfile include_tail
    fold: ^[res ipath]
            @(
                let fqn [] extend: ipath fullfile
                ?: fileExists?: fqn
                    reduced: fqn
                    res
            )
        nil include_paths

; Function: full_include_paths
; Description: Iterates include file through include paths
; and verify existence
; Syntax: full_include_paths: acc incfile
;   acc - Positive result accumulator
;   incfile - primary name of include file to resolve

func :private full_include_paths [acc incfile]
    let hit [] resolve_include_location: incfile
    ?: hit
        list_extend!: acc hit
        exception: format: "Can't find include file {} " [incfile]

; Function: include_reducer
; Description: Checks cache for existence, otherwise
; parses and processes the include file
; Syntax: include_reducer: acc incfile
;   acc - result accumulator
;   incfile - fully qualified include file

func :private include_reducer [acc incfile]
    ; If not already in cache
    ?: nil?: getd: include_cache incfile nil
        @(
            set_extend!: include_cache incfile
            list_extend!:
                acc
                build_handler_and_run: {
                    :source     incfile
                    :command    :procinc
                }
        )
        ; Else ignore re-processing
        acc

; Function: preload_langhdrs
; Description: Load the foidlrt and langcore headers to
; feed the symbol table prior to parsing tokens
; Syntax: preload_langhdrs: state
;   state - Command line flags and state settings

func :private preload_langhdrs [state]
    ; For each include file, determine path based on
    ; include searching
    let res []  fold: full_include_paths list_inst!: include_rthdrs
    fold: include_reducer list_inst!: res
    state

; Function: process_includes
; Description: Pulls all the include statements from
; the source token list into 1 list
; Syntax: process_includes: state
;   state - Command line flags and state settings

func :private process_includes [state]
    map_extend!: state :inclist process_tpdwn_inc: get: state :tokens


; Function: var_to_symboltable
; Description: Puts variable entry to symbol table
; Syntax: func_to_symboltable: ntok arglist acnt
;   ntok    - The symbol token found for the variable

func :private var_to_symboltable [symtablectxt ntok]
    let ts [] get: ntok :token_str
    st_add_symbolref:
        ts
        map_extend!:    ; Extend so name is direct member of ref
            type_instance: :varref symtablectxt ntok
            NAME ts

; Function: func_to_symboltable
; Description: Puts unction entry to symbol table
; Syntax: func_to_symboltable: ntok arglist acnt
;   ntok    - The symbol token found for the function
;   arglist - List of argument symbol strings (if any)
;   acnt    - Count of args in arglist

func :private func_to_symboltable [symtablectxt ntok arglist acnt]
    let ts [] get: ntok :token_str
    st_add_symbolref:
        ts
        map_extend!:
            map_extend!:    ; Extend so name is direct member of ref
                type_instance: :funcref symtablectxt ntok
                NAME ts
            ARGCNT acnt

; Function: consume_include
; Description: Process a single include file header, primarily
; to populate scoped symtbol table with var and func references
; Syntax: consume_include: state
;   state - Scoped variation of the base file command and state info

func :private consume_include [state]
    ; push a new table identified by the module name being
    ; processed
    let mname [] get: state :module_name
    st_push_table: mname
    let ochan [] get: state :outchannel
    ; Walk tokens and capture vars and funcs in symbol table context
    process_tpdwn_vf: state (var_to_symboltable mname) (func_to_symboltable mname)
    state

; Function: consume_includes
; Description: For any includes found in main source, check if
; already consumed and if not, consume (assuming it can be found)
; Syntax: consume_includes: state
;   state - Command line flags and state settings

func :private consume_includes [state]
    let ilist [] get: get: state :inclist :incs
    let res []
            fold:
                full_include_paths
                list_inst!: ilist
    fold: include_reducer list_inst!: res
    consume_include: state
    state

; Function: symboltrees_print
; Description: Print symbol tree(s)
; Syntax: symboltrees_print: state
;   state - Command line flags and state settings

func :private symboltrees_print [state]
    dump_symbols:
    state

; Function: token_capture
; Description: Helper function to accumulate
; tokens if index is not in set of indexes
; Syntax: token_capture: rlist iset toks acc ndx
;   rlist - Result list of tokens
;   iset -  Set of token indexes to ignore
;   toks - Token list
;   acc - Ignored
;   ndx - Current index counter

func :private token_capture [rlist iset toks acc ndx]
    let ntok [] get: toks ndx
    ?: get: iset ndx
        acc
        extend!: rlist ntok

; Function: reverse_tokens
; Description: Reverse token list, ignoring includes and drop original
; Syntax: reverse_tokens: state
;   state - Command line flags and state settings

func :private reverse_tokens [state]
    let iset [] get: get: state :inclist :inci
    let rlist [] list_inst!:
    let toks [] get: state :tokens
    fold: (token_capture rlist iset toks) zero series: zero count: toks 1
    map_extend!: state :tokens rlist

; Function: parse_tokens
; Description: Bottom up parse of tokens to resulting AST

func :private parse_tokens   [state]
    map_extend!:
        state
        :ast
        parse: get: state :tokens get: state :module_name

; Function: ast_print
; Description: Prints the AST to screen

func :private ast_print   [state]
    print_ast: get: state :ast
    state

func :private parse_tree   [state]
    printnl!: "Creating PAST"
    state

func :private produce_ir   [state]
    printnl!: "Creating 3AC"
    state

func :private ir_print   [state]
    printnl!: "Print 3AC"
    state

func :private emit_llvm   [state]
    printnl!: "Emit LLVM"
    state

; Function: func_genhdr_writer
; Description: Writes out function signature in genheader processing
; Syntax: func_genhdr_writer: ochan stok arglist acnt
;   ochan   - The output channel
;   ntok    - The symbol found for the function
;   arglist - List of argument symbols (if any)
;   acnt    - Count of args in arglist

func :private func_genhdr [ochan ntok arglist acnt]
    write!: ochan "func "
    write!: ochan get: ntok :token_str
    write!: ochan " ["
    ?: >: acnt zero
        fold:
            ^[acc indx]
                @(
                    ?: >: indx 0
                        write!: ochan " "
                        nil
                    write!: ochan get: arglist indx
                    acc
                )
            nil series: 0 acnt 1
        nil
    write!: ochan "]"
    write!: ochan nlchr

; Function: var_genhdr
; Description: Writes out variable signature in genheader processing
; Syntax: func_genhdr_writer: ochan ntok
;   ochan   - The output channel
;   ntok    - The symbol found for the variable

func :private var_genhdr [ochan ntok]
    write!: ochan "var "
    write!: ochan get: ntok :token_str
    write!: ochan " any"
    write!: ochan nlchr

; Function: genheader
; Description: Generates module, public vars and public funcs
; to designated output
; Rules:
;   Comment block
;   'module name'       : name is already part of state
;   'var name Any'      : name is token right after var if not :private
;   'func name [args]'  : name is token right after func if not :private
; Syntax: genheader: state
;   state - Command line flags and state settings

func :private genheader [state]
    let ochan [] get: state :outchannel
    write_comment_block_and_module: ochan get: state :module_name
    ; Walk tokens and spit vars and funcs
    process_tpdwn_vf: state (var_genhdr ochan) (func_genhdr ochan)
    write!: ochan nlchr
    state

; Function: close_output
; Description: Closes, if not stdout, the output channel
; Syntax: close_output: state
;   state - Command line flags and state settings

func :private close_output [state]
    let output [] get: state :outchannel
    ?: =: output cout
        state
        close!: output
    state

; Function: skip_step
; Description: Passthrough step returns state and
; is used when the debugging (-pt, -pa, -pi) are not specified

func :private skip_step [state]
    state

; Place holder for dev
func :private faux_step [s state]
    print!: "Place holder step for " printnl!: s
    state

; Function: build_handler
; Description: Constructs a compilation pipeline based on the
; command line information
; Syntax: build_handler: cmap
;   cmap - Command line flags and settings
; pipeline examples
; If :compile command
;   lex_source          - inserts tokens into state
;   [token_print]       - optionally print tokens
;   setup_output        - opens an output channel
;   setup_symboltable   - sets a symbol table up
;   [preload_langincs]  - Preprocess foidlrt and langcore if !-r
;   process_includes    - seperate include(s) from main tokens
;   consume_includes    - consume includes, calls consume_include for each
;   [symboltrees_print] - *NEW* prints all symbol tables (long output)
;   reverse_tokens      - support bottom up parsing
;   parse_tokens        - parse the tokens to abstract syntax tree (AST)
;   [ast_print]         - optionally prints AST
;   parse_tree          - walks AST, refactor, generate parse abstract syntax tree (PAST)
;   produce_ir          - walks PAST, creates three address code list (3AC)
;   [ir_print]          - optionally print 3AC
;   emit_llvm           - Emits LLVM-IR from 3AC
;   close_output        - Closes output channel
;
; If :genhdr command
;   lex_source          - inserts tokens into state
;   [token_print]       - optionally print tokens
;   setup_output        - opens an output channel
;   genheader           - Emits foidl defs
;   close_output        - Closes output channel
;
; If :procinc command   - Called internally to process include files
;   lex_source          - inserts tokens into state
;   consume_include     - Generate symbol table references


func build_handler_and_run [cmap]

    let state []
        {
            :cmap           cmap
            :source         get: cmap :source
        }

    ; Capture the include paths if coming in with :compile

    match pipeline get: cmap :command
        | :compile  [
                    capture_includes
                    (lex_source false)
                    ?: get: cmap :printtoks
                        token_print
                        skip_step
                    setup_output
                    setup_symboltable
                    ?: get: cmap :compiling_runtime
                        skip_step
                        preload_langhdrs
                    process_includes
                    consume_includes
                    ?: get: cmap :printsyms
                        symboltrees_print
                        skip_step
                    reverse_tokens
                    parse_tokens
                    ?: get: cmap :printast
                        ast_print
                        skip_step
                    (faux_step "parse_tree")
                    (faux_step "produce_ir")
                    ?: get: cmap :printir
                        (faux_step "ir_print")
                        skip_step
                    (faux_step "emit_llvm")
                    close_output
                    ]
        | :genhdr   [
                    (lex_source false)
                    ?: get: cmap :printtoks
                        token_print
                        skip_step
                    setup_output
                    genheader
                    close_output
                    ]
        | :procinc  [
                    (lex_source true)
                    consume_include
                    ]
        | :default  exception:
                        format:
                            "Invalid switch found in build_handler {}"
                            [%0]
    fold: ^[acc el] (el acc) state pipeline

